BACKUP ~A7-ChaosSorcerer/backup~
AUTHOR ~Argent77~
VERSION ~2.0~

ALWAYS
  OUTER_SET SPELL_REV = (MOD_IS_INSTALLED ~setup-spell_rev.tp2~ 0) ? 1 : 0
  INCLUDE ~%MOD_FOLDER%/lib/functions.tph~
END

README ~%MOD_FOLDER%/readme/readme-%LANGUAGE%.txt~ ~%MOD_FOLDER%/readme/readme.txt~

LANGUAGE ~English~
         ~english~
         ~%MOD_FOLDER%/languages/english/setup.tra~
         ~%MOD_FOLDER%/languages/english/kit.tra~
         ~%MOD_FOLDER%/languages/english/spells.tra~
         ~%MOD_FOLDER%/languages/english/misc.tra~
         ~%MOD_FOLDER%/languages/english/items.tra~
LANGUAGE ~Deutsch~
         ~german~
         ~%MOD_FOLDER%/languages/german/setup.tra~
         ~%MOD_FOLDER%/languages/german/kit.tra~
         ~%MOD_FOLDER%/languages/german/spells.tra~
         ~%MOD_FOLDER%/languages/german/misc.tra~
         ~%MOD_FOLDER%/languages/german/items.tra~


BEGIN @0  // "Chaos Sorcerer" kit
  REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee~) @500 // This component requires BG:EE, BG2:EE, EET or IWD:EE to be installed.
  DESIGNATED 0

  INCLUDE ~%MOD_FOLDER%/lib/kit.tph~
  INCLUDE ~%MOD_FOLDER%/lib/spells.tph~

<<<<<<<< .../inline/marker.dat
marker
>>>>>>>>
  COPY ~.../inline/marker.dat~ ~override/a7_csmain.mrk~

  ACTION_IF (~%WEIDU_OS%~ STR_EQ ~win32~) BEGIN
    AT_EXIT ~del /f /q override\a7_csmain.mrk >nul 2>&1~
  END ELSE BEGIN
    AT_EXIT ~rm -f override/a7_csmain.mrk >/dev/null 2>&1~
  END


BEGIN @10 // Recreate "Chaos Weave" spell selection lists
  REQUIRE_COMPONENT ~setup-A7-ChaosSorcerer.tp2~ 0 @501 // This component requires the main component to be installed.
  REQUIRE_PREDICATE (NOT FILE_EXISTS ~override/a7_csmain.mrk~) @502 // This component is intended to be called separately if needed.
  NO_LOG_RECORD
  DESIGNATED 10

  INCLUDE ~%MOD_FOLDER%/lib/spell_selection.tph~


BEGIN @20 // Make "Wild Mage" unavailable to character creation
  REQUIRE_COMPONENT ~setup-A7-ChaosSorcerer.tp2~ 0 @501 // This component requires the main component to be installed.
  REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~K_M_H.2DA~) @503  // This component is not supported by the game.
  DESIGNATED 20

  INCLUDE ~%MOD_FOLDER%/lib/disable_wildmage.tph~


BEGIN @30 // Add "Primordial Artifacts"
  REQUIRE_COMPONENT ~setup-A7-ChaosSorcerer.tp2~ 0 @501 // This component requires the main component to be installed.
  DESIGNATED 30

  INCLUDE ~%MOD_FOLDER%/lib/items.tph~
