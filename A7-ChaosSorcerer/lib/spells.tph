// *** Spells for Chaos Sorcerer ***

INCLUDE ~%MOD_FOLDER%/lib/spells_base.tph~


// spell: Chaotic Weave
INCLUDE ~%MOD_FOLDER%/lib/spell_selection.tph~
COPY ~%MOD_FOLDER%/spells/a7_sp01.spl~ ~override~
  SAY NAME1 @2000 // Chaotic Weave
  SAY UNIDENTIFIED_DESC @2001 // Chaotic Weave description

  // applying 1st tier selections
  LPF apply_opcode_214
  INT_VAR header = 1 prob_min = 93  prob_max = 100
  STR_VAR resource = ~A7_SP1D~
  END
  LPF apply_opcode_214
  INT_VAR header = 1 prob_min = 73  prob_max = 92
  STR_VAR resource = ~A7_SP1C~
  END
  LPF apply_opcode_214
  INT_VAR header = 1 prob_min = 40  prob_max = 72
  STR_VAR resource = ~A7_SP1B~
  END
  LPF apply_opcode_214
  INT_VAR header = 1 prob_min = 0  prob_max = 39
  STR_VAR resource = ~A7_SP1A~
  END

  // applying 2nd tier selections
  LPF apply_opcode_214
  INT_VAR header = 2 prob_min = 83  prob_max = 100
  STR_VAR resource = ~A7_SP2C~
  END
  LPF apply_opcode_214
  INT_VAR header = 2 prob_min = 50  prob_max = 82
  STR_VAR resource = ~A7_SP2B~
  END
  LPF apply_opcode_214
  INT_VAR header = 2 prob_min = 0  prob_max = 49
  STR_VAR resource = ~A7_SP2A~
  END

  // applying 3rd tier selections
  LPF apply_opcode_214
  INT_VAR header = 3 prob_min = 80  prob_max = 100
  STR_VAR resource = ~A7_SP3C~
  END
  LPF apply_opcode_214
  INT_VAR header = 3 prob_min = 40  prob_max = 79
  STR_VAR resource = ~A7_SP3B~
  END
  LPF apply_opcode_214
  INT_VAR header = 3 prob_min = 0  prob_max = 39
  STR_VAR resource = ~A7_SP3A~
  END

  // applying 4th tier selections
  LPF apply_opcode_214
  INT_VAR header = 4 prob_min = 50  prob_max = 100
  STR_VAR resource = ~A7_SP4B~
  END
  LPF apply_opcode_214
  INT_VAR header = 4 prob_min = 0  prob_max = 49
  STR_VAR resource = ~A7_SP4A~
  END

  // applying 5th tier selections
  LPF apply_opcode_214
  INT_VAR header = 5 prob_min = 33  prob_max = 100
  STR_VAR resource = ~A7_SP5B~
  END
  LPF apply_opcode_214
  INT_VAR header = 5 prob_min = 0  prob_max = 32
  STR_VAR resource = ~A7_SP5A~
  END

  // applying 6th tier selections
  LPF apply_opcode_214
  INT_VAR header = 6 prob_min = 0  prob_max = 100
  STR_VAR resource = ~A7_SP6A~
  END

COPY ~%MOD_FOLDER%/spells/a7_sp01b.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp01c.bam~ ~override~


// HLA: Chaotic Eruption
COPY ~%MOD_FOLDER%/spells/a7_sp10.spl~ ~override~
  SAY NAME1 @2100 // Chaotic Eruption
  SAY UNIDENTIFIED_DESC @2101 // Chaotic Eruption description

COPY ~%MOD_FOLDER%/spells/a7_sp10b.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp10c.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1b.bmp~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2b.bmp~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1a.vvc~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1b.vvc~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2a.vvc~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2b.vvc~ ~override~

COPY ~%MOD_FOLDER%/creatures/a7_cel1a.cre~ ~override~
  SAY NAME1 @10000  // Chaos Elemental
  SAY NAME2 @10000  // Chaos Elemental

COPY ~%MOD_FOLDER%/creatures/a7_cel2a.cre~ ~override~
  SAY NAME1 @10001  // Greater Chaos Elemental
  SAY NAME2 @10001  // Greater Chaos Elemental

COPY ~%MOD_FOLDER%/spells/a7_cel1a.eff~ ~override~
     ~%MOD_FOLDER%/spells/a7_cel2a.eff~ ~override~
     ~%MOD_FOLDER%/spells/a7_cel3.spl~ ~override~

COPY ~%MOD_FOLDER%/spells/a7_cel1.spl~ ~override~
  SAY NAME1 @5000 // Confusion
  SAY UNIDENTIFIED_DESC @5001 // ...

COPY ~%MOD_FOLDER%/spells/a7_cel2.spl~ ~override~
  SAY NAME1 @5050 // Mass Confusion
  SAY UNIDENTIFIED_DESC @5051 // ...

COMPILE ~%MOD_FOLDER%/scripts/a7_cel1a.baf~
        ~%MOD_FOLDER%/scripts/a7_cel1x.baf~
        ~%MOD_FOLDER%/scripts/a7_cel2a.baf~
        ~%MOD_FOLDER%/scripts/a7_cel2x.baf~


// Spell: Conjure Chaos Elemental
COPY ~%MOD_FOLDER%/spells/a7_sp03.spl~ ~override~
    SAY NAME1 @2050 // Conjure Chaos Elemental
    SAY UNIDENTIFIED_DESC @2051 // ...

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~spin906.spl~) BEGIN
  COPY ~%MOD_FOLDER%/spells/spin906.spl~ ~override~
  APPEND ~spell.ids~ ~3906 THREE_ROUND_ENCHANTMENT_IMMUNITY~ UNLESS ~THREE_ROUND_ENCHANTMENT_IMMUNITY~
END

COMPILE ~%MOD_FOLDER%/scripts/a7_cel1b.baf~
        ~%MOD_FOLDER%/scripts/a7_cel1y.baf~

COPY ~%MOD_FOLDER%/creatures/a7_cel1b.cre~ ~override~
  SAY NAME1 @10000  // Chaos Elemental
  SAY NAME2 @10000  // Chaos Elemental

COPY ~%MOD_FOLDER%/spells/a7_sp03a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp03b.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp03c.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp03.eff~ ~override~


// Spell: Conjure Greater Chaos Elemental
COPY ~%MOD_FOLDER%/spells/a7_sp04.spl~ ~override~
    SAY NAME1 @2150 // Conjure Greater Chaos Elemental
    SAY UNIDENTIFIED_DESC @2151 // ...

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~spin906.spl~) BEGIN
  COPY ~%MOD_FOLDER%/spells/spin906.spl~ ~override~
  APPEND ~spell.ids~ ~3906 THREE_ROUND_ENCHANTMENT_IMMUNITY~ UNLESS ~THREE_ROUND_ENCHANTMENT_IMMUNITY~
END

COMPILE ~%MOD_FOLDER%/scripts/a7_cel2b.baf~
        ~%MOD_FOLDER%/scripts/a7_cel2y.baf~

COPY ~%MOD_FOLDER%/creatures/a7_cel2b.cre~ ~override~
  SAY NAME1 @10001  // Greater Chaos Elemental
  SAY NAME2 @10001  // Greater Chaos Elemental

COPY ~%MOD_FOLDER%/spells/a7_sp04a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp04b.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp04c.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp04.eff~ ~override~


// Adding Chaos Sorcerer specific wild surges
ACTION_IF (FILE_EXISTS_IN_GAME ~wildmag.2da~) BEGIN
  OUTER_SET kit_id = IDS_OF_SYMBOL(~kit~ ~A7_CHAOS_SORCERER~)

  // Reading original surges from table
  COPY_EXISTING - ~wildmag.2da~ ~override~
    COUNT_2DA_COLS numCols
    COUNT_2DA_ROWS numCols numRows
    PATCH_IF (numRows > 53) BEGIN
      READ_2DA_ENTRY 49 1 numCols resref_surge_50
      READ_2DA_ENTRY 49 2 numCols strref_surge_50
      READ_2DA_ENTRY 53 1 numCols resref_surge_54
      READ_2DA_ENTRY 53 2 numCols strref_surge_54
    END ELSE BEGIN
      TEXT_SPRINT resref_surge_50 ~~
      SET strref_surge_50 = "-1"
      TEXT_SPRINT resref_surge_54 ~~
      SET strref_surge_54 = "-1"
    END

  ACTION_IF (NOT ~%resref_surge_50%~ STR_EQ ~~) BEGIN
    // Specific Wild Surge 50: Summon Chaos Elemental (controllable)
    COPY ~%MOD_FOLDER%/spells/a7_wm01.spl~ ~override~
      LPF a7_update_effect_params
      INT_VAR 
        opcode            = 177
        match_parameter1  = 1
        parameter1        = 9999
        new_parameter1    = kit_id
      END

    COPY ~%MOD_FOLDER%/spells/a7_wm1a.eff~ ~override~
      WRITE_LONG 0x1c strref_surge_50

    COPY ~%MOD_FOLDER%/spells/a7_wm1b.eff~ ~override~
      WRITE_ASCIIE 0x30 ~%resref_surge_50%~ (8)

    COPY ~%MOD_FOLDER%/spells/a7_wm1c.eff~ ~override~
      SAY 0x1c @10004 // Chaos Elemental summoned

    COPY ~%MOD_FOLDER%/spells/a7_wm1d.eff~ ~override~

    COPY ~%MOD_FOLDER%/creatures/a7_cel1c.cre~ ~override~
      SAY NAME1 @10000  // Chaos Elemental
      SAY NAME2 @10000  // Chaos Elemental

    COPY_EXISTING ~wildmag.2da~ ~override~
      SET_2DA_ENTRY 49 1 numCols ~A7_WM01~
      SET_2DA_ENTRY 49 2 numCols ~-1~
    BUT_ONLY
  END


  ACTION_IF (NOT ~%resref_surge_54%~ STR_EQ ~~) BEGIN
    // Specific Wild Surge 54: Call forth Greater Chaos Elemental (hostile)
    COPY ~%MOD_FOLDER%/spells/a7_wm02.spl~ ~override~
      LPF a7_update_effect_params
      INT_VAR 
        opcode            = 177
        match_parameter1  = 1
        parameter1        = 9999
        new_parameter1    = kit_id
      END

    COPY ~%MOD_FOLDER%/spells/a7_wm1a.eff~ ~override/a7_wm2a.eff~
      WRITE_LONG 0x1c strref_surge_54

    COPY ~%MOD_FOLDER%/spells/a7_wm1b.eff~ ~override/a7_wm2b.eff~
      WRITE_ASCIIE 0x30 ~%resref_surge_54%~ (8)

    COPY ~%MOD_FOLDER%/spells/a7_wm1c.eff~ ~override/a7_wm2c.eff~
      SAY 0x1c @10005 // Oops, a rift... Is that a chaos creature?!

    COPY ~%MOD_FOLDER%/spells/a7_wm2d.eff~ ~override~

    COPY ~%MOD_FOLDER%/creatures/a7_cel2c.cre~ ~override~
      SAY NAME1 @10001  // Greater Chaos Elemental
      SAY NAME2 @10001  // Greater Chaos Elemental

    COPY_EXISTING ~wildmag.2da~ ~override~
      SET_2DA_ENTRY 53 1 numCols ~A7_WM02~
      SET_2DA_ENTRY 53 2 numCols ~-1~
    BUT_ONLY
  END

  // For debugging purposes only:
  // COPY_EXISTING ~wildmag.2da~ ~override~
    // FOR (row = 0; row < numRows; ++row) BEGIN
      // READ_2DA_ENTRY row 1 numCols value
      // PATCH_IF (NOT ~%value%~ STR_EQ ~*~) BEGIN
        // SET_2DA_ENTRY row 1 numCols ~A7_WM01~
        // SET_2DA_ENTRY row 2 numCols ~-1~
      // END
    // END
END
