// *** Spells for Chaos Sorcerer ***

INCLUDE ~%MOD_FOLDER%/lib/spells_base.tph~
INCLUDE ~%MOD_FOLDER%/lib/spell_selection.tph~


// spell: Chaotic Weave
COPY ~%MOD_FOLDER%/spells/a7_sp01.spl~ ~override~
  SAY NAME1 @2000 // Chaotic Weave
  SAY UNIDENTIFIED_DESC @2001 // Chaotic Weave description

  // Adding spell selection lists
  // Order of entries: ability header, prob_min, prob_max => table name
  PATCH_DEFINE_ASSOCIATIVE_ARRAY tiers BEGIN
    // Tier 1 (Distribution: 33/25/18/13/7/4 percent per probability group)
    ~1~, ~99~, ~100~ => ~A7_SP1T~
    ~1~, ~98~, ~98~  => ~A7_SP1S~
    ~1~, ~96~, ~97~  => ~A7_SP1R~
    ~1~, ~94~, ~95~  => ~A7_SP1Q~
    ~1~, ~92~, ~93~  => ~A7_SP1P~
    ~1~, ~89~, ~91~  => ~A7_SP1O~
    ~1~, ~85~, ~88~  => ~A7_SP1N~
    ~1~, ~81~, ~84~  => ~A7_SP1M~
    ~1~, ~76~, ~80~  => ~A7_SP1L~
    ~1~, ~70~, ~75~  => ~A7_SP1K~
    ~1~, ~64~, ~69~  => ~A7_SP1J~
    ~1~, ~58~, ~63~  => ~A7_SP1I~
    ~1~, ~52~, ~57~  => ~A7_SP1H~
    ~1~, ~46~, ~51~  => ~A7_SP1G~
    ~1~, ~40~, ~45~  => ~A7_SP1F~
    ~1~, ~33~, ~39~  => ~A7_SP1E~
    ~1~, ~25~, ~32~  => ~A7_SP1D~
    ~1~, ~17~, ~24~  => ~A7_SP1C~
    ~1~,  ~9~, ~16~  => ~A7_SP1B~
    ~1~,  ~0~,  ~8~  => ~A7_SP1A~
    // Tier 2 (Distribution: 33/25/18/13/7/4 percent per probability group)
    ~2~, ~99~, ~100~ => ~A7_SP2T~
    ~2~, ~98~, ~98~  => ~A7_SP2S~
    ~2~, ~96~, ~97~  => ~A7_SP2R~
    ~2~, ~94~, ~95~  => ~A7_SP2Q~
    ~2~, ~92~, ~93~  => ~A7_SP2P~
    ~2~, ~89~, ~91~  => ~A7_SP2O~
    ~2~, ~85~, ~88~  => ~A7_SP2N~
    ~2~, ~81~, ~84~  => ~A7_SP2M~
    ~2~, ~76~, ~80~  => ~A7_SP2L~
    ~2~, ~70~, ~75~  => ~A7_SP2K~
    ~2~, ~64~, ~69~  => ~A7_SP2J~
    ~2~, ~58~, ~63~  => ~A7_SP2I~
    ~2~, ~52~, ~57~  => ~A7_SP2H~
    ~2~, ~46~, ~51~  => ~A7_SP2G~
    ~2~, ~40~, ~45~  => ~A7_SP2F~
    ~2~, ~33~, ~39~  => ~A7_SP2E~
    ~2~, ~25~, ~32~  => ~A7_SP2D~
    ~2~, ~17~, ~24~  => ~A7_SP2C~
    ~2~,  ~9~, ~16~  => ~A7_SP2B~
    ~2~,  ~0~,  ~8~  => ~A7_SP2A~
    // Tier 3 (Distribution: 33/25/18/13/7/4 percent per probability group)
    ~3~, ~99~, ~100~ => ~A7_SP3T~
    ~3~, ~98~, ~98~  => ~A7_SP3S~
    ~3~, ~96~, ~97~  => ~A7_SP3R~
    ~3~, ~94~, ~95~  => ~A7_SP3Q~
    ~3~, ~92~, ~93~  => ~A7_SP3P~
    ~3~, ~89~, ~91~  => ~A7_SP3O~
    ~3~, ~85~, ~88~  => ~A7_SP3N~
    ~3~, ~81~, ~84~  => ~A7_SP3M~
    ~3~, ~76~, ~80~  => ~A7_SP3L~
    ~3~, ~70~, ~75~  => ~A7_SP3K~
    ~3~, ~64~, ~69~  => ~A7_SP3J~
    ~3~, ~58~, ~63~  => ~A7_SP3I~
    ~3~, ~52~, ~57~  => ~A7_SP3H~
    ~3~, ~46~, ~51~  => ~A7_SP3G~
    ~3~, ~40~, ~45~  => ~A7_SP3F~
    ~3~, ~33~, ~39~  => ~A7_SP3E~
    ~3~, ~25~, ~32~  => ~A7_SP3D~
    ~3~, ~17~, ~24~  => ~A7_SP3C~
    ~3~,  ~9~, ~16~  => ~A7_SP3B~
    ~3~,  ~0~,  ~8~  => ~A7_SP3A~
    // Tier 4 (Distribution: 33/25/18/13/7/4 percent per probability group)
    ~4~, ~98~, ~100~ => ~A7_SP4S~
    ~4~, ~96~, ~97~  => ~A7_SP4R~
    ~4~, ~94~, ~95~  => ~A7_SP4Q~
    ~4~, ~92~, ~93~  => ~A7_SP4P~
    ~4~, ~89~, ~91~  => ~A7_SP4O~
    ~4~, ~85~, ~88~  => ~A7_SP4N~
    ~4~, ~81~, ~84~  => ~A7_SP4M~
    ~4~, ~76~, ~80~  => ~A7_SP4L~
    ~4~, ~70~, ~75~  => ~A7_SP4K~
    ~4~, ~64~, ~69~  => ~A7_SP4J~
    ~4~, ~58~, ~63~  => ~A7_SP4I~
    ~4~, ~52~, ~57~  => ~A7_SP4H~
    ~4~, ~46~, ~51~  => ~A7_SP4G~
    ~4~, ~40~, ~45~  => ~A7_SP4F~
    ~4~, ~33~, ~39~  => ~A7_SP4E~
    ~4~, ~25~, ~32~  => ~A7_SP4D~
    ~4~, ~17~, ~24~  => ~A7_SP4C~
    ~4~,  ~9~, ~16~  => ~A7_SP4B~
    ~4~,  ~0~,  ~8~  => ~A7_SP4A~
    // Tier 5 (Distribution: 50/33/17 percent per probability group)
    ~5~, ~83~, ~100~ => ~A7_SP5G~
    ~5~, ~72~, ~82~ => ~A7_SP5F~
    ~5~, ~61~, ~71~ => ~A7_SP5E~
    ~5~, ~50~, ~60~ => ~A7_SP5D~
    ~5~, ~34~, ~49~ => ~A7_SP5C~
    ~5~, ~17~, ~33~ => ~A7_SP5B~
    ~5~, ~0~, ~16~ => ~A7_SP5A~
    // Tier 6 (Distribution: 100 percent per probability group)
    ~6~, ~0~, ~100~ => ~A7_SP6A~
  END

  PATCH_PHP_EACH tiers AS numbers => resref BEGIN
    LPF apply_opcode_214
    INT_VAR
      header = $numbers(~0~)
      prob_min = $numbers(~1~)
      prob_max = $numbers(~2~)
    STR_VAR
      resource = EVAL ~%resref%~
    END
  END

COPY ~%MOD_FOLDER%/spells/a7_sp01b.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp01c.bam~ ~override~


// HLA: Chaotic Eruption
ACTION_IF (EE_V2) BEGIN
  OUTER_SPRINT A7_SP10_SPL ~%MOD_FOLDER%/spells/a7_sp10.spl~
END ELSE BEGIN
  OUTER_SPRINT A7_SP10_SPL ~%MOD_FOLDER%/spells/pre20/a7_sp10.spl~
END
COPY ~%A7_SP10_SPL%~ ~override~
  SAY NAME1 @2100 // Chaotic Eruption
  SAY UNIDENTIFIED_DESC @2101 // Chaotic Eruption description

COPY ~%MOD_FOLDER%/spells/a7_sp10b.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp10c.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1b.bmp~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1c.bmp~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2b.bmp~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2c.bmp~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1a.vvc~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1b.vvc~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs1c.vvc~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2a.vvc~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2b.vvc~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs2c.vvc~ ~override~

COPY ~%MOD_FOLDER%/creatures/a7_cel1a.cre~ ~override~
  SAY NAME1 @10000  // Chaos Elemental
  SAY NAME2 @10000  // Chaos Elemental

COPY ~%MOD_FOLDER%/creatures/a7_cel2a.cre~ ~override~
  SAY NAME1 @10001  // Greater Chaos Elemental
  SAY NAME2 @10001  // Greater Chaos Elemental

COPY ~%MOD_FOLDER%/spells/a7_cel1a.eff~ ~override~
     ~%MOD_FOLDER%/spells/a7_cel2a.eff~ ~override~
     ~%MOD_FOLDER%/spells/a7_cel3.spl~ ~override~

COPY ~%MOD_FOLDER%/spells/a7_cel1.spl~ ~override~
  SAY NAME1 @5000 // Confusion
  SAY UNIDENTIFIED_DESC @5001 // ...

COPY ~%MOD_FOLDER%/spells/a7_cel2.spl~ ~override~
  SAY NAME1 @5050 // Mass Confusion
  SAY UNIDENTIFIED_DESC @5051 // ...

COMPILE ~%MOD_FOLDER%/scripts/a7_cel1a.baf~
        ~%MOD_FOLDER%/scripts/a7_cel1x.baf~
        ~%MOD_FOLDER%/scripts/a7_cel2a.baf~
        ~%MOD_FOLDER%/scripts/a7_cel2x.baf~


// Spell: Conjure Chaos Elemental
COPY ~%MOD_FOLDER%/spells/a7_sp03.spl~ ~override~
    SAY NAME1 @2050 // Conjure Chaos Elemental
    SAY UNIDENTIFIED_DESC @2051 // ...

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~spin906.spl~) BEGIN
  COPY ~%MOD_FOLDER%/spells/spin906.spl~ ~override~
  APPEND ~spell.ids~ ~3906 THREE_ROUND_ENCHANTMENT_IMMUNITY~ UNLESS ~THREE_ROUND_ENCHANTMENT_IMMUNITY~
END

COMPILE ~%MOD_FOLDER%/scripts/a7_cel1b.baf~
        ~%MOD_FOLDER%/scripts/a7_cel1y.baf~

COPY ~%MOD_FOLDER%/creatures/a7_cel1b.cre~ ~override~
  SAY NAME1 @10000  // Chaos Elemental
  SAY NAME2 @10000  // Chaos Elemental

ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~%MOD_FOLDER%/spells/iwdee/a7_sp03b.bam~ ~override~
       ~%MOD_FOLDER%/spells/iwdee/a7_sp03c.bam~ ~override~
END ELSE BEGIN
  COPY ~%MOD_FOLDER%/spells/a7_sp03b.bam~ ~override~
       ~%MOD_FOLDER%/spells/a7_sp03c.bam~ ~override~
END
COPY ~%MOD_FOLDER%/spells/a7_sp03a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp03.eff~ ~override~
     ~%MOD_FOLDER%/items/a7_ring1.itm~ ~override~


// Spell: Conjure Greater Chaos Elemental
COPY ~%MOD_FOLDER%/spells/a7_sp04.spl~ ~override~
    SAY NAME1 @2150 // Conjure Greater Chaos Elemental
    SAY UNIDENTIFIED_DESC @2151 // ...

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~spin906.spl~) BEGIN
  COPY ~%MOD_FOLDER%/spells/spin906.spl~ ~override~
  APPEND ~spell.ids~ ~3906 THREE_ROUND_ENCHANTMENT_IMMUNITY~ UNLESS ~THREE_ROUND_ENCHANTMENT_IMMUNITY~
END

COMPILE ~%MOD_FOLDER%/scripts/a7_cel2b.baf~
        ~%MOD_FOLDER%/scripts/a7_cel2y.baf~

COPY ~%MOD_FOLDER%/creatures/a7_cel2b.cre~ ~override~
  SAY NAME1 @10001  // Greater Chaos Elemental
  SAY NAME2 @10001  // Greater Chaos Elemental

ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~%MOD_FOLDER%/spells/iwdee/a7_sp04b.bam~ ~override~
       ~%MOD_FOLDER%/spells/iwdee/a7_sp04c.bam~ ~override~
END ELSE BEGIN
  COPY ~%MOD_FOLDER%/spells/a7_sp04b.bam~ ~override~
       ~%MOD_FOLDER%/spells/a7_sp04c.bam~ ~override~
END
COPY ~%MOD_FOLDER%/spells/a7_sp04a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp04.eff~ ~override~


// Spell: Stabilize
// installing status icon
OUTER_SET stat_index = 0
COPY_EXISTING ~statdesc.2da~ ~override~
  READ_2DA_ENTRIES_NOW statdesc 1
  FOR (row = 3; row < statdesc; ++row) BEGIN
    READ_2DA_ENTRY_FORMER statdesc row 0 value
    PATCH_IF (IS_AN_INT ~value~ && value > stat_index) BEGIN
      stat_index = value
    END
  END
  SET stat_index += 1
BUT_ONLY
OUTER_SET stat_strref = RESOLVE_STR_REF(@2702)  // Surge Protection
APPEND ~statdesc.2da~ ~%stat_index%         %stat_strref%      A7_SP05D~ UNLESS ~A7_SP05D~

// installing projectile
ADD_PROJECTILE ~%MOD_FOLDER%/spells/a7_wrdbl.pro~
// Cosmetic adjustment: more descriptive name for missile entry
COPY_EXISTING ~missile.ids~ ~override~
  REPLACE_TEXTUALLY ~%a7_wrdbl%[ %TAB%]+a7_wrdbl~ ~%a7_wrdbl% Weird_Field_Blue~
BUT_ONLY

// installing spell
COPY ~%MOD_FOLDER%/spells/a7_sp05.spl~ ~override~
  SAY NAME1 @2700 // Stabilize
  SAY UNIDENTIFIED_DESC @2701 // Stabilize description
  LPF ALTER_SPELL_EFFECT
    INT_VAR
      match_opcode = 142  // Display portrait icon
      parameter2 = stat_index
  END
  LPF ALTER_SPELL_HEADER
    INT_VAR
      projectile = a7_wrdbl
  END

// installing spell scroll
COPY ~%MOD_FOLDER%/spells/a7_sc01.itm~ ~override~
  SAY NAME2 @2700 // Stabilize
  SAY IDENTIFIED_DESC @2701 // Stabilize description

COPY ~%MOD_FOLDER%/spells/a7_sp05a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp05b.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp05c.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp05d.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_wrdbl.bam~ ~override~

ACTION_IF (GAME_INCLUDES ~tob~) BEGIN
  // Add spell scroll to Chromatic Demon in Watcher's Keep
  COPY_EXISTING ~gorchr.cre~ ~override~
    ADD_CRE_ITEM ~a7_sc01~ #0 #0 #0 ~none~ ~inv~
  BUT_ONLY
END ELSE ACTION_IF (GAME_IS ~iwdee~) BEGIN
  // Add spell scroll to the drow mage Malavon
  COPY_EXISTING ~malavon.cre~ ~override~
    ADD_CRE_ITEM ~a7_sc01~ #0 #0 #0 ~none~ ~inv~
  BUT_ONLY
END


// Spell: Wildfire
// installing spell
COPY ~%MOD_FOLDER%/spells/a7_sp06.spl~ ~override~
  SAY NAME1 @2750 // Wildfire
  SAY UNIDENTIFIED_DESC @2751 // Wildfire description

// creating spell table
LAF a7_create_spell_list_file
  INT_VAR
    level_1 = ~100~
    level_2 = ~100~
    level_3 = ~100~
    level_4 = ~100~
    level_5 = ~100~
    level_6 = ~100~
    level_7 = ~100~
    level_8 = ~100~
    level_9 = ~0~
  STR_VAR
    fixed_table = ~~  // no fixed spells
    spell_table = ~spell_table~
    file_name = ~a7_sp06.2da~
END

// installing spell scroll
COPY ~%MOD_FOLDER%/spells/a7_sc02.itm~ ~override~
  SAY NAME2 @2750 // Wildfire
  SAY IDENTIFIED_DESC @2751 // Wildfire description

COPY ~%MOD_FOLDER%/spells/a7_sp06a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp06b.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_sp06c.bam~ ~override~

ACTION_IF (GAME_INCLUDES ~tob~) BEGIN
  // Add spell scroll to Odamaron's chamber in Sendai's Lair
  COPY_EXISTING ~ar6110.are~ ~override~
    LPF ADD_AREA_ITEM
      INT_VAR
        container_to_add_to = 3
        charges1 = 1
      STR_VAR
        item_to_add = ~a7_sc02~
    END
  BUT_ONLY
END ELSE ACTION_IF (GAME_IS ~iwdee~) BEGIN
  // Add spell scroll to upper story of Castle Maluradek
  COPY_EXISTING ~ar9712.are~ ~override~
    LPF ADD_AREA_ITEM
      INT_VAR
        container_to_add_to = 6
        charges1 = 1
      STR_VAR
        item_to_add = ~a7_sc02~
    END
  BUT_ONLY
END


// Adding Chaos Sorcerer specific wild surges
ACTION_IF (FILE_EXISTS_IN_GAME ~wildmag.2da~) BEGIN
  OUTER_SET kit_id = IDS_OF_SYMBOL(~kit~ ~A7_CHAOS_SORCERER~)

  // Reading original surges from table
  COPY_EXISTING ~wildmag.2da~ ~override~
    COUNT_2DA_COLS numCols
    COUNT_2DA_ROWS numCols numRows
    PATCH_IF (numRows > 53) BEGIN
      READ_2DA_ENTRY 49 1 numCols resref_surge_50
      READ_2DA_ENTRY 49 2 numCols strref_surge_50
      READ_2DA_ENTRY 53 1 numCols resref_surge_54
      READ_2DA_ENTRY 53 2 numCols strref_surge_54
    END ELSE BEGIN
      SPRINT resref_surge_50 ~~
      SET strref_surge_50 = "-1"
      SPRINT resref_surge_54 ~~
      SET strref_surge_54 = "-1"
    END
  BUT_ONLY

  ACTION_IF (NOT ~%resref_surge_50%~ STR_EQ ~~) BEGIN
    // Specific Wild Surge 50: Summon Chaos Elemental (controllable)
    COPY ~%MOD_FOLDER%/spells/a7_wm01.spl~ ~override~
      LPF a7_update_effect_params
      INT_VAR
        opcode            = 177
        match_parameter1  = 1
        parameter1        = 9999
        new_parameter1    = kit_id
      END

    COPY ~%MOD_FOLDER%/spells/a7_wm1a.eff~ ~override~
      WRITE_LONG 0x1c strref_surge_50

    COPY ~%MOD_FOLDER%/spells/a7_wm1b.eff~ ~override~
      WRITE_ASCIIE 0x30 ~%resref_surge_50%~ (8)

    COPY ~%MOD_FOLDER%/spells/a7_wm1c.eff~ ~override~
      SAY 0x1c @10004 // Chaos Elemental summoned

    COPY ~%MOD_FOLDER%/spells/a7_wm1d.eff~ ~override~

    COPY ~%MOD_FOLDER%/creatures/a7_cel1c.cre~ ~override~
      SAY NAME1 @10000  // Chaos Elemental
      SAY NAME2 @10000  // Chaos Elemental

    COPY_EXISTING ~wildmag.2da~ ~override~
      SET_2DA_ENTRY 49 1 numCols ~A7_WM01~
      SET_2DA_ENTRY 49 2 numCols ~-1~
    BUT_ONLY
  END


  ACTION_IF (NOT ~%resref_surge_54%~ STR_EQ ~~) BEGIN
    // Specific Wild Surge 54: Call forth Greater Chaos Elemental (hostile)
    COPY ~%MOD_FOLDER%/spells/a7_wm02.spl~ ~override~
      LPF a7_update_effect_params
      INT_VAR
        opcode            = 177
        match_parameter1  = 1
        parameter1        = 9999
        new_parameter1    = kit_id
      END

    COPY ~%MOD_FOLDER%/spells/a7_wm1a.eff~ ~override/a7_wm2a.eff~
      WRITE_LONG 0x1c strref_surge_54

    COPY ~%MOD_FOLDER%/spells/a7_wm1b.eff~ ~override/a7_wm2b.eff~
      WRITE_ASCIIE 0x30 ~%resref_surge_54%~ (8)

    COPY ~%MOD_FOLDER%/spells/a7_wm1c.eff~ ~override/a7_wm2c.eff~
      SAY 0x1c @10005 // Oops, a rift... Is that a chaos creature?!

    COPY ~%MOD_FOLDER%/spells/a7_wm2d.eff~ ~override~

    COPY ~%MOD_FOLDER%/creatures/a7_cel2c.cre~ ~override~
      SAY NAME1 @10001  // Greater Chaos Elemental
      SAY NAME2 @10001  // Greater Chaos Elemental

    COPY_EXISTING ~wildmag.2da~ ~override~
      SET_2DA_ENTRY 53 1 numCols ~A7_WM02~
      SET_2DA_ENTRY 53 2 numCols ~-1~
    BUT_ONLY
  END
END
