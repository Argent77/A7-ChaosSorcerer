// *** Spells for all Wild Magic related kits and classes ***

// Relocate Nahal's Reckless Dweomer (SPWI124)
LAF a7_find_free_spell_slot INT_VAR type = 2 level = 1 RET slot resref END
OUTER_TEXT_SPRINT spell_nrd_resref ~%resref%~
ACTION_IF (slot >= 0) BEGIN
  // updating SPELL.IDS entries
  COPY_EXISTING ~SPELL.IDS~ ~override~
    LPF a7_update_spell_ids_code INT_VAR code = 2124 new_code = slot END

  COPY_EXISTING ~SPWI124.SPL~ ~override/%spell_nrd_resref%.SPL~
    WRITE_BYTE 0x25 10  // Primary type: Wild Mage
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124B~ resref_dst = EVAL ~A7_SP04B~ END
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124C~ resref_dst = EVAL ~A7_SP04C~ END
    LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 321  // Remove effects by resource
      target = 1    // Self
    STR_VAR
      resource = ~SPWI124~
    END

  COPY_EXISTING ~SPWI124A.BAM~ ~override/A7_SP04A.BAM~
                ~SPWI124B.BAM~ ~override/A7_SP04B.BAM~
                ~SPWI124C.BAM~ ~override/A7_SP04C.BAM~

  // updating references in game resources
  COPY_EXISTING ~CONTINGX.2DA~ ~override~
                ~BGEE.LUA~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124~ resref_dst = EVAL ~%spell_nrd_resref%~ END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING_REGEXP ~.+\.bcs~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124~ resref_dst = EVAL ~%spell_nrd_resref%~ END
  BUT_ONLY

  COPY_EXISTING_REGEXP ~.+\.dlg~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124~ resref_dst = EVAL ~%spell_nrd_resref%~ END
  BUT_ONLY

  COPY_EXISTING_REGEXP ~.+\.cre~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124~ resref_dst = EVAL ~%spell_nrd_resref%~ END
  BUT_ONLY

  // spell should appear on character spell selection screen, except on pre-2.0 games
  ACTION_IF (FILE_EXISTS_IN_GAME ~engine.lua~) BEGIN
    COPY_EXISTING ~hidespl.2da~ ~override~
      LPF a7_remove_2da_row INT_VAR column = 0 STR_VAR match = EVAL ~%spell_nrd_resref%~ END
    BUT_ONLY
  END ELSE BEGIN
    APPEND ~hidespl.2da~ ~%spell_nrd_resref% 1     0          0~ UNLESS ~%spell_nrd_resref%~
  END
END ELSE BEGIN
  FAIL ~Failed to find unoccupied level 1 spell slot.~
END


// Install new revision of Chaos Shield (SPWI222)
LAF a7_find_free_spell_slot INT_VAR type = 2 level = 3 RET slot resref END
OUTER_TEXT_SPRINT spell_chaos_shield_resref ~%resref%~
ACTION_IF (slot >= 0) BEGIN
  // updating SPELL.IDS entries
  COPY_EXISTING ~SPELL.IDS~ ~override~
    LPF a7_update_spell_ids_code INT_VAR code = 2222 new_code = slot END
    // Improved Chaos Shield is redirected to Chaos Shield
    LPF a7_update_spell_ids_code INT_VAR code = 2723 new_code = slot END

  COPY_EXISTING - ~spwi222.spl~ ~override~
    READ_LONG NAME1 strref_name

  COPY ~%MOD_FOLDER%/spells/spwi2xx.spl~ ~override/%spell_chaos_shield_resref%.SPL~
    WRITE_LONG NAME1 strref_name  // use original name
    SAY UNIDENTIFIED_DESC @2651   // new Chaos Shield description
    // opcode 321: resource SPWI222 -> new resref
    LPF a7_update_spell_effect_params
    INT_VAR
      match_resource = 1
      opcode = 321
    STR_VAR
      resource = ~SPWI222~
      new_resource = EVAL ~%spell_chaos_shield_resref%~
    END
    PATCH_IF (NOT FILE_EXISTS_IN_GAME ~splstate.ids~) BEGIN
      // pre-patch 2.0/1.4 game found
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 328 END
    END

  // updating references in game resources
  COPY_EXISTING ~BGEE.LUA~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI222~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
    LPF a7_update_resref STR_VAR resref_src = ~SPWI723~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING_REGEXP ~.+\.bcs~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI222~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
    LPF a7_update_resref STR_VAR resref_src = ~SPWI723~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
  BUT_ONLY

  COPY_EXISTING_REGEXP ~.+\.dlg~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI222~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
    LPF a7_update_resref STR_VAR resref_src = ~SPWI723~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
  BUT_ONLY

  COPY_EXISTING_REGEXP ~.+\.cre~ ~override~
    PATCH_IF (INDEX_BUFFER(~SPWI222~) > 0) BEGIN
      ADD_KNOWN_SPELL ~%spell_chaos_shield_resref%~ #2 ~wizard~
    END
  BUT_ONLY

  
  // spell should appear on character spell selection screen
  COPY_EXISTING ~hidespl.2da~ ~override~
    LPF a7_remove_2da_row INT_VAR column = 0 STR_VAR match = EVAL ~%spell_chaos_shield_resref%~ END
  BUT_ONLY
END ELSE BEGIN
  FAIL ~Failed to find unoccupied level 3 spell slot.~
END


// Install Surge Control (SPWI124)
COPY ~%MOD_FOLDER%/spells/spwi124.spl~ ~override~
  SAY NAME1 @2500 // Surge Control
  SAY UNIDENTIFIED_DESC @2501 // Surge Control description
  // opcode 321: resource SPWI222 -> new resref
  LPF a7_update_spell_effect_params
  INT_VAR
    match_resource = 1
    opcode = 321
  STR_VAR
    resource = ~SPWI222~
    new_resource = EVAL ~%spell_chaos_shield_resref%~
  END

COPY ~%MOD_FOLDER%/spells/spwi124b.bam~ ~override~
     ~%MOD_FOLDER%/spells/spwi124c.bam~ ~override~

// spell does not appear on character spell selection screen, except on pre-2.0 games
ACTION_IF (FILE_EXISTS ~engine.lua~) BEGIN
  APPEND ~hidespl.2da~ ~SPWI124 1     0          0~ UNLESS ~SPWI124~
END ELSE BEGIN
  COPY_EXISTING ~hidespl.2da~ ~override~
    LPF a7_remove_2da_row INT_VAR column = 0 STR_VAR match = ~SPWI124~ END
  BUT_ONLY
END

// Updating spellbooks
COPY_EXISTING_REGEXP ~.+\.cre~ ~override~
  PATCH_IF (INDEX_BUFFER(~%spell_nrd_resref%~) > 0) BEGIN
    ADD_KNOWN_SPELL ~SPWI124~ #0 ~wizard~
  END
BUT_ONLY


// Install Unluck (SPWI222)
COPY ~%MOD_FOLDER%/spells/spwi222.spl~ ~override~
  SAY NAME1 @2550 // Unluck
  SAY UNIDENTIFIED_DESC @2551 // Unluck description

COPY ~%MOD_FOLDER%/spells/spwi222b.bam~ ~override~
     ~%MOD_FOLDER%/spells/spwi222c.bam~ ~override~

// spell should appear on character spell selection screen
COPY_EXISTING ~hidespl.2da~ ~override~
  LPF a7_remove_2da_row INT_VAR column = 0 STR_VAR match = ~SPWI222~ END
BUT_ONLY


// Install Nahal's Wildstrike (SPWI723)
COPY ~%MOD_FOLDER%/spells/spwi723.spl~ ~override~
  SAY NAME1 @2600 // Nahal's Wildstrike
  SAY UNIDENTIFIED_DESC @2601 // Nahal's Wildstrike description
  PATCH_IF (NOT FILE_EXISTS_IN_GAME ~splstate.ids~) BEGIN
    // pre-patch 2.0/1.4 game found
    LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 328 END
  END

COPY ~%MOD_FOLDER%/spells/spwi723b.bam~ ~override~
     ~%MOD_FOLDER%/spells/spwi723c.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs3a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs3a.vvc~ ~override~

// spell should appear on character spell selection screen
COPY_EXISTING ~hidespl.2da~ ~override~
  LPF a7_remove_2da_row INT_VAR column = 0 STR_VAR match = ~SPWI723~ END
BUT_ONLY


// pre-2.0 patched specific adjustments
ACTION_IF (/*GAME_IS ~iwdee~ &&*/ NOT FILE_EXISTS_IN_GAME ~engine.lua~) BEGIN
  // generating clabma01.2da if needed
  ACTION_IF (NOT FILE_EXISTS ~clabma01.2da~) BEGIN
    COPY_EXISTING - ~clabfi01.2da~ ~override~
      COUNT_2DA_COLS numCols

    COPY ~%MOD_FOLDER%/tables/stub.2da~ ~override/clabma01.2da~

    // table header
    OUTER_TEXT_SPRINT line ~ ~
    OUTER_FOR (idx = 1; idx < numCols; ++idx) BEGIN
      ACTION_IF (idx < 10) BEGIN
        OUTER_TEXT_SPRINT line ~%line%           %idx%~
      END ELSE BEGIN
        OUTER_TEXT_SPRINT line ~%line%          %idx%~
      END
    END
    APPEND ~clabma01.2da~ ~%line%~

    // table content
    OUTER_FOR (abil = 1; abil < 7; ++abil) BEGIN
      OUTER_TEXT_SPRINT line ~ABILITY%abil%~
      OUTER_FOR (idx = 1; idx < numCols; ++idx) BEGIN
        ACTION_IF (idx = 1) BEGIN
          OUTER_TEXT_SPRINT line ~%line%    ****~
        END ELSE BEGIN
          OUTER_TEXT_SPRINT line ~%line%        ****~
        END
      END
      APPEND ~clabma01.2da~ ~%line%~
    END
  END

  COPY ~%MOD_FOLDER%/spells/a7_cs05.eff~ ~override~
    WRITE_ASCIIE 0x30 ~%spell_nrd_resref%~ (8)

  COPY ~%MOD_FOLDER%/spells/a7_cs06.eff~ ~override~
    WRITE_ASCIIE 0x30 ~%spell_chaos_shield_resref%~ (8)

  COPY ~%MOD_FOLDER%/spells/a7_cs05.spl~ ~override~
       ~%MOD_FOLDER%/spells/a7_cs06.spl~ ~override~

  // these spells have to be added manually
  OUTER_TEXT_SPRINT give_abil_0 ~AP_A7_CS05~
  OUTER_TEXT_SPRINT give_abil_1 ~AP_A7_CS06~
  OUTER_SET give_abil = 2
  OUTER_SET abilIdx = 0

  // Adding custom abilities
  COPY_EXISTING ~clabma01.2da~ ~override~
    COUNT_2DA_COLS numCols
    COUNT_2DA_ROWS numCols numRows
    FOR (row = 0; row < numRows; ++row) BEGIN
      READ_2DA_ENTRY row 1 numCols value
      PATCH_IF (~%value%~ STR_EQ ~****~) BEGIN
        TEXT_SPRINT resref EVAL ~%give_abil_%abilIdx%%~
        SET_2DA_ENTRY row 1 numCols ~%resref%~
        SET abilIdx += 1
      END
      PATCH_IF (abilIdx = give_abil) BEGIN
        SET row = numRows
      END
    END
    PRETTY_PRINT_2DA
  BUT_ONLY

  // In the unlikely case all slots are occupied already
  OUTER_WHILE (abilIdx < give_abil) BEGIN
    OUTER_SET idx = numRows + 1
    OUTER_TEXT_SPRINT line ~ABILITY%idx%~
    OUTER_FOR (idx = 1; idx < numCols; ++idx) BEGIN
      ACTION_IF (idx = 1) BEGIN
        OUTER_TEXT_SPRINT resref EVAL ~%give_abil_%abilIdx%%~
        OUTER_TEXT_SPRINT line ~%line%    %resref%~
      END ELSE ACTION_IF (idx = 2) BEGIN
        OUTER_TEXT_SPRINT line ~%line%  ****~
      END ELSE BEGIN
        OUTER_TEXT_SPRINT line ~%line%        ****~
      END
    END
    APPEND ~clabma01.2da~ ~%line%~
    OUTER_SET abilIdx += 1
    OUTER_SET numRows += 1
  END
END
