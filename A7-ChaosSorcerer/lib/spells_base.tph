// *** Spells for all Wild Magic related kits and classes ***

// Relocate Nahal's Reckless Dweomer (SPWI124)
LAF a7_find_free_spell_slot INT_VAR type = 2 level = 1 RET slot resref END
OUTER_TEXT_SPRINT spell_nrd_resref ~%resref%~
ACTION_IF (slot >= 0) BEGIN
  // updating SPELL.IDS entries
  COPY_EXISTING ~SPELL.IDS~ ~override~
    LPF a7_update_spell_ids_code INT_VAR code = 2124 new_code = slot END

  COPY_EXISTING ~SPWI124.SPL~ ~override/%spell_nrd_resref%.SPL~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124B~ resref_dst = EVAL ~A7_SP04B~ END
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124C~ resref_dst = EVAL ~A7_SP04C~ END
    LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 321  // Remove effects by resource
      target = 1    // Self
    STR_VAR
      resource = ~SPWI124~
    END

  COPY_EXISTING ~SPWI124A.BAM~ ~override/A7_SP04A.BAM~
                ~SPWI124B.BAM~ ~override/A7_SP04B.BAM~
                ~SPWI124C.BAM~ ~override/A7_SP04C.BAM~

  // updating references in game resources
  COPY_EXISTING ~CONTINGX.2DA~ ~override~
                ~BGEE.LUA~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124~ resref_dst = EVAL ~%spell_nrd_resref%~ END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING_REGEXP ~.+\.bcs~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124~ resref_dst = EVAL ~%spell_nrd_resref%~ END
  BUT_ONLY

  COPY_EXISTING_REGEXP ~.+\.dlg~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124~ resref_dst = EVAL ~%spell_nrd_resref%~ END
  BUT_ONLY

  COPY_EXISTING_REGEXP ~.+\.cre~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI124~ resref_dst = EVAL ~%spell_nrd_resref%~ END
  BUT_ONLY

  // spell does not appear in character spell selection screens
  APPEND ~hidespl.2da~ ~%spell_nrd_resref% 1     0          0~ UNLESS ~%spell_nrd_resref%~
END ELSE BEGIN
  FAIL ~Failed to find unoccupied level 1 spell slot.~
END


// Install new revision of Chaos Shield (SPWI222)
LAF a7_find_free_spell_slot INT_VAR type = 2 level = 3 RET slot resref END
OUTER_TEXT_SPRINT spell_chaos_shield_resref ~%resref%~
ACTION_IF (slot >= 0) BEGIN
  // updating SPELL.IDS entries
  COPY_EXISTING ~SPELL.IDS~ ~override~
    LPF a7_update_spell_ids_code INT_VAR code = 2222 new_code = slot END
    // Improved Chaos Shield is redirected to Chaos Shield
    LPF a7_update_spell_ids_code INT_VAR code = 2723 new_code = slot END

  COPY_EXISTING - ~spwi222.spl~ ~override~
    READ_LONG NAME1 strref_name

  COPY ~%MOD_FOLDER%/spells/spwi2xx.spl~ ~override/%spell_chaos_shield_resref%.SPL~
    WRITE_LONG NAME1 strref_name  // use original name
    SAY UNIDENTIFIED_DESC @2651   // new Chaos Shield description
    // opcode 321: resource SPWI222 -> new resref
    LPF a7_update_spell_effect_params
    INT_VAR
      match_resource = 1
      opcode = 321
    STR_VAR
      resource = ~SPWI222~
      new_resource = EVAL ~%spell_chaos_shield_resref%~
    END
    PATCH_IF (NOT FILE_EXISTS_IN_GAME ~splstate.ids~) BEGIN
      // pre-patch 2.0/1.4 game found
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 328 END
    END

  // updating references in game resources
  COPY_EXISTING ~BGEE.LUA~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI222~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
    LPF a7_update_resref STR_VAR resref_src = ~SPWI723~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING_REGEXP ~.+\.bcs~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI222~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
    LPF a7_update_resref STR_VAR resref_src = ~SPWI723~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
  BUT_ONLY

  COPY_EXISTING_REGEXP ~.+\.dlg~ ~override~
    LPF a7_update_resref STR_VAR resref_src = ~SPWI222~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
    LPF a7_update_resref STR_VAR resref_src = ~SPWI723~ resref_dst = EVAL ~%spell_chaos_shield_resref%~ END
  BUT_ONLY

  COPY_EXISTING_REGEXP ~.+\.cre~ ~override~
    PATCH_IF (INDEX_BUFFER(~SPWI222~) > 0) BEGIN
      ADD_KNOWN_SPELL ~%spell_chaos_shield_resref%~ #2 ~wizard~
    END
  BUT_ONLY
  
  // spell does not appear in character spell selection screens
  APPEND ~hidespl.2da~ ~%spell_chaos_shield_resref% 1     0          0~ UNLESS ~%spell_chaos_shield_resref%~
END ELSE BEGIN
  FAIL ~Failed to find unoccupied level 3 spell slot.~
END


// Install Surge Control (SPWI124)
COPY ~%MOD_FOLDER%/spells/spwi124.spl~ ~override~
  SAY NAME1 @2500 // Surge Control
  SAY UNIDENTIFIED_DESC @2501 // Surge Control description
  // opcode 321: resource SPWI222 -> new resref
  LPF a7_update_spell_effect_params
  INT_VAR
    match_resource = 1
    opcode = 321
  STR_VAR
    resource = ~SPWI222~
    new_resource = EVAL ~%spell_chaos_shield_resref%~
  END

COPY ~%MOD_FOLDER%/spells/spwi124b.bam~ ~override~
     ~%MOD_FOLDER%/spells/spwi124c.bam~ ~override~

// spell does not appear in character spell selection screens
APPEND ~hidespl.2da~ ~SPWI124 1     0          0~ UNLESS ~SPWI124~

// Updating spellbooks
COPY_EXISTING_REGEXP ~.+\.cre~ ~override~
  PATCH_IF (INDEX_BUFFER(~%spell_nrd_resref%~) > 0) BEGIN
    ADD_KNOWN_SPELL ~SPWI124~ #0 ~wizard~
  END
BUT_ONLY


// Install Unluck (SPWI222)
COPY ~%MOD_FOLDER%/spells/spwi222.spl~ ~override~
  SAY NAME1 @2550 // Unluck
  SAY UNIDENTIFIED_DESC @2551 // Unluck description

COPY ~%MOD_FOLDER%/spells/spwi222b.bam~ ~override~
     ~%MOD_FOLDER%/spells/spwi222c.bam~ ~override~

// spell does not appear in character spell selection screens
APPEND ~hidespl.2da~ ~SPWI222 1     0          0~ UNLESS ~SPWI222~


// Install Nahal's Wildstrike (SPWI723)
COPY ~%MOD_FOLDER%/spells/spwi723.spl~ ~override~
  SAY NAME1 @2600 // Nahal's Wildstrike
  SAY UNIDENTIFIED_DESC @2601 // Nahal's Wildstrike description
  PATCH_IF (NOT FILE_EXISTS_IN_GAME ~splstate.ids~) BEGIN
    // pre-patch 2.0/1.4 game found
    LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 328 END
  END

COPY ~%MOD_FOLDER%/spells/spwi723b.bam~ ~override~
     ~%MOD_FOLDER%/spells/spwi723c.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs3a.bam~ ~override~
     ~%MOD_FOLDER%/spells/a7_cs3a.vvc~ ~override~

// spell does not appear in character spell selection screens
APPEND ~hidespl.2da~ ~SPWI723 1     0          0~ UNLESS ~SPWI723~


// Update Wild Mage CLAB
OUTER_TEXT_SPRINT clab_resref ~CLABMA01~
COPY_EXISTING - ~kitlist.2da~ ~override~
  COUNT_2DA_COLS numCols
  COUNT_2DA_ROWS numCols numRows
  FOR (row = 0; row < numRows; ++row) BEGIN
    READ_2DA_ENTRY row 1 numCols value
    PATCH_IF (~%value%~ STRING_MATCHES_REGEXP ~WILD_?MAGE~ = 0) BEGIN
      READ_2DA_ENTRY row 5 numCols clab_resref
      SET row = numRows
    END
  END
IF_EXISTS

ACTION_IF (~%clab_resref%~ STR_EQ ~~ || NOT FILE_EXISTS_IN_GAME ~%clab_resref%.2DA~) BEGIN
  OUTER_SET numCols = 50
  COPY_EXISTING - ~clabfi01.2da~ ~override~
    COUNT_2DA_COLS numCols
  IF_EXISTS

  COPY ~%MOD_FOLDER%/tables/clab_stub.2da~ ~override/%clab_resref%.2DA~

  // writing table header
  OUTER_TEXT_SPRINT line ~~
  OUTER_FOR (col = 1; col < numCols; ++col) BEGIN
    ACTION_IF (col < 10) BEGIN
      OUTER_TEXT_SPRINT line ~%line%%col%           ~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT line ~%line%%col%          ~
    END
  END
  OUTER_PATCH ~~ BEGIN LPF a7_trim STR_VAR line = EVAL ~%line%~ RET line END END
  OUTER_TEXT_SPRINT line ~            %line%~
  APPEND ~%clab_resref%.2DA~ ~%line%~

  // generating empty lines
  OUTER_FOR (row = 1; row < 7; ++row) BEGIN
    OUTER_TEXT_SPRINT line ~ABILITY%row%    ~
    OUTER_FOR (col = 1; col < numCols; ++col) BEGIN
      OUTER_TEXT_SPRINT line ~%line%****        ~
    END
    OUTER_PATCH ~~ BEGIN LPF a7_trim STR_VAR line = EVAL ~%line%~ RET line END END
    APPEND ~%clab_resref%.2DA~ ~%line%~
  END
END

COPY_EXISTING ~%clab_resref%.2da~ ~override~
  SET index = 0
  TEXT_SPRINT abil_0 ~GA_%spell_nrd_resref%~
  TEXT_SPRINT abil_1 ~GA_%spell_chaos_shield_resref%~
  SET abil = 2
  COUNT_2DA_COLS numCols
  COUNT_2DA_ROWS numCols numRows
  FOR (row = 0; row < numRows; ++row) BEGIN
    READ_2DA_ENTRY row 1 numCols value
    PATCH_IF (~%value%~ STRING_MATCHES_REGEXP ~\*.*~ = 0) BEGIN
      TEXT_SPRINT entry EVAL ~%abil_%index%%~
      SET_2DA_ENTRY row 1 numCols ~%entry%~
      SET index += 1
    END
    PATCH_IF (index = abil) BEGIN
      SET row = numRows
    END
  END
BUT_ONLY

// if rows have run out before all entries could be added
OUTER_WHILE (index < abil) BEGIN
  OUTER_TEXT_SPRINT line ~~
  OUTER_FOR (col = 0; col < numCols - 2; ++col) BEGIN
    OUTER_TEXT_SPRINT line ~        ****%line%~
  END
  OUTER_TEXT_SPRINT entry EVAL ~%abil_%index%%~
  OUTER_TEXT_SPRINT line ~%entry%  %line%~
  OUTER_TEXT_SPRINT line ~ABILITY%row%    ~
  APPEND ~%clab_resref%.2da~ ~%line%~
  OUTER_SET index += 1
  OUTER_SET row += 1
END
