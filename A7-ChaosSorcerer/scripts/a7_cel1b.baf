// Combat script for controllable Chaos Elementals

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("Busy", "LOCALS")
  !See([ENEMY])
  HPPercentLT(Myself, 90)
  RandomNum(10, 1)
THEN
  RESPONSE #100
    ForceSpellRES("A7_CEL3", Myself)  // Cure Wounds
    Continue()
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("Busy", "LOCALS")
  !See([ENEMY])
  !Range([PC], 20)
  !Range([FAMILIAR], 20)
THEN
  RESPONSE #10
    SetGlobalTimer("Busy", "LOCALS", 5)
    MoveToObject(LastSummonerOf(Myself))
END

IF
  ActionListEmpty()
  AttackedBy([ENEMY], 0)
THEN
  RESPONSE #100
    AttackOneRound(LastAttackerOf(Myself))
END

IF
  ActionListEmpty()
  See([ENEMY.0.0.0.0.0.MASK_LAWFUL])
  !StateCheck(LastSeenBy(Myself), STATE_CHARMED)
THEN
  RESPONSE #25
    ForceSpellRES("A7_CEL1", LastSeenBy(Myself))  // Chaos
    AttackReevaluate(LastSeenBy(Myself), 15)
  RESPONSE #75
    AttackReevaluate(LastSeenBy(Myself), 15)
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself), STATE_CHARMED)
THEN
  RESPONSE #10
    ForceSpellRES("A7_CEL1", LastSeenBy(Myself))  // Chaos
    AttackReevaluate(LastSeenBy(Myself), 15)
  RESPONSE #90
    AttackReevaluate(LastSeenBy(Myself), 15)
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("Busy", "LOCALS")
  OR(2)
    !See([ENEMY])
    StateCheck([ENEMY], STATE_CHARMED)
  OR(2)
    Range([PC], 20)
    Range([FAMILIAR], 20)
  RandomNumLT(4, 4)
THEN
  RESPONSE #100
    SetGlobalTimer("Busy", "LOCALS", 5)
    RandomWalkContinuous()
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("Busy", "LOCALS")
  OR(2)
    !See([ENEMY])
    StateCheck([ENEMY], STATE_CHARMED)
  OR(2)
    Range([PC], 20)
    Range([FAMILIAR], 20)
  RandomNum(4, 4)
THEN
  RESPONSE #100
    SetGlobalTimer("Busy", "LOCALS", 5)
    RandomTurn()
END
